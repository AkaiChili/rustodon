document = _{
    (non_text | text)*
}

text = {
    (!non_text ~ ANY)+
}

non_text = _{
    emoticon | hashtag | mention | link | line_break
}

line_break = {
    "\r\n" | "\n"
}

emoticon = {
    ":"
    ~ emoticon_name
    ~ ":"
}

emoticon_name = {
    (LETTER | NUMBER | DASH_PUNCTUATION)+
}

hashtag = {
    symbol_prefix
    ~ "#" ~ hashtag_name
    ~ symbol_suffix
}

hashtag_name = {
    (!(symbol_identifiers | symbol_suffix) ~ ANY){1,32}
}

mention = {
    symbol_prefix
    ~ "@" ~ (
          (mention_username ~ !"@")
        | (mention_username ~ "@" ~ mention_domain)
    )
    ~ symbol_suffix
}

mention_username = {
    (!(symbol_identifiers | symbol_suffix) ~ ANY){1,32}
}

mention_domain = {
    (!(symbol_identifiers | symbol_suffix) ~ ANY){1,60}
}

symbol_prefix = {
    WHITE_SPACE | (!symbol_identifiers ~ PUNCTUATION) | SOI
}

symbol_suffix = _{
    &((!symbol_identifiers ~ PUNCTUATION)+ ~ (WHITE_SPACE | EOI) | WHITE_SPACE | EOI)
}

symbol_identifiers = _{
    "@" | "#"
}

link = {
    symbol_prefix ~ link_schema ~ link_tail
}

link_schema = {
    ALPHABETIC+ ~ ":" ~ "//"?
}

link_tail = {
    (!((!("/" | "-") ~ PUNCTUATION)+ ~ (WHITE_SPACE | EOI) | WHITE_SPACE) ~ ANY)+
}